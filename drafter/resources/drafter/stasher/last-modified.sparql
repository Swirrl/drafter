prefix dcterms: <http://purl.org/dc/terms/>
prefix drafter: <http://publishmydata.com/def/drafter/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix draft: <http://publishmydata.com/graphs/drafter/draft>
prefix draftset: <http://publishmydata.com/def/drafter/draftset/>

SELECT ?livemod ?livever ?draftmod ?draftver WHERE {
  OPTIONAL {
    SELECT ?livemod ?livever WHERE {
      VALUES ?graph {}
      GRAPH <http://publishmydata.com/graphs/drafter/drafts> {
        ?graph a drafter:ManagedGraph ;
               drafter:isPublic "true"^^xsd:boolean ;
               dcterms:modified ?livemod .
      }
      # TODO we don't really want this to be optional, it would be better to
      # ensure it always exists.
      OPTIONAL {
        GRAPH <http://publishmydata.com/graphs/drafter/endpoints> {
          drafter:public drafter:version ?livever .
        }
      }
    }
  }

  OPTIONAL {
    SELECT ?draftmod ?draftver WHERE {
      VALUES ?graph {}
      GRAPH <http://publishmydata.com/graphs/drafter/drafts> {
        ?graph a drafter:DraftGraph ;
               dcterms:modified ?draftmod .
        ?graph drafter:inDraftSet ?ds .
        ?ds drafter:version ?draftver .
      }
    }
  }
} ORDER BY DESC(?livemod) DESC(?draftmod) LIMIT 1
